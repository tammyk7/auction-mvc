plugins {
    id 'java'
}

group = 'weareadaptive.com'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.aeron:aeron-all:1.41.2'
    implementation 'io.aeron:aeron-test-support:1.41.2'
    implementation 'io.vertx:vertx-core:4.3.7'
    implementation 'org.slf4j:slf4j-api:2.0.7'
    implementation 'ch.qos.logback:logback-classic:1.4.7'

    testImplementation 'org.mockito:mockito-core:5.3.1'
    testImplementation 'io.vertx:vertx-junit5:4.3.7'
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

tasks.jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes["Main-Class"] = "com.weareadaptive.cluster.ClusterMain"
    }

    // Include all runtime dependencies
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    // Exclude the META-INF directories of the dependencies, to avoid a merge conflict
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}

tasks.withType(Test).configureEach {
    jvmArgs '--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED'
}

test {
    useJUnitPlatform()
}